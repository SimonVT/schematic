apply plugin: 'android'
apply plugin: 'checkstyle'
apply plugin: 'android-apt'

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:0.10.+'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
  }
}

dependencies {
  apt project(':schematic-compiler')
  compile project(':schematic')
  compile libraries.supportv4
  compile libraries.butterknife
}

apt {
  arguments {
    schematicOutPackage 'net.simonvt.schematic.sample.provider'
  }
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    minSdkVersion = parent.ext.minSdkVersion
    targetSdkVersion = parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.applicationVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

// Workaround because compiler and sample is in the same gradle project.
// DO NOT USE WHEN PULLING FROM MAVEN REPO
android.applicationVariants.all { variant ->
  variant.javaCompile.dependsOn ':schematic-compiler:jar'
}
